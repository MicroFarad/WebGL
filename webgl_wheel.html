<!--
WebGL Raycasting Experiment
Copyright (C) 2016 Kyle Gagner
All Rights Reserved
-->
<!DOCTYPE html>
<html>
<head>
	<meta charset="utf-8" />
	<title>WebGL Raycasting Experiment</title>
</head>
<body>
<script id="vertex_shader" type="shader">
	precision mediump float;
	attribute vec3 pos;
	uniform float aspectratio;
	uniform float fieldofview;
	varying vec3 p;
	void main()
	{
		p = vec3(pos.x, pos.y / aspectratio, 1.0 / tan(fieldofview / 2.0));
		gl_Position = vec4(pos, 1.0);
	}
</script>
<script id="fragment_shader" type="shader">
	#define MAXITER 100
	#define EPSILON 0.01
	#define ESCAPE 100.0
	#define ROSE_QUARTZ vec3(0.969, 0.792, 0.788)
	#define SERENITY vec3(0.573, 0.659, 0.820)
	#define LIGHT vec3(5, 10, 10)
	#define AMBIENT 0.2
	precision mediump float;
	uniform vec3 eyeposition;
	uniform mat3 raytransform;
	varying vec3 p;

	float torus(in vec3 c, out vec3 n, in float r1, in float r2)
	{
		float a = length(c.xz)-r1;
		vec3 to;
		to.xz = (length(c.xz)-r1) * normalize(c.xz);
		to.y = c.y;
		n = normalize(to);
		return length(to)-r2;
	}

	float box(in vec3 c, out vec3 n, in vec3 corner)
	{
		vec3 s = sign(c);
		vec3 d = abs(c) - corner;
		float m = max(d.x, max(d.y, d.z));
		if(m > 0.0)
		{
			n = vec3(0.0, 0.0, 0.0);
		}
		else if(d.x == m)
		{
			n = vec3(s.x, 0.0, 0.0);
		}
		else if(d.y == m)
		{
			n = vec3(0.0, s.y, 0.0);
		}
		else
		{
			n = vec3(0.0, 0.0, s.z);
		}
		vec3 p = max(d, 0.0);
		n += s * normalize(p);
		
		return min(m, 0.0) + length(p);
	}

	float sphere(in vec3 c, out vec3 n, in float r)
	{
		n = normalize(c);
		return length(c) - r;
	}

	float cylinder(in vec3 c, out vec3 n, in vec2 rim)
	{
		vec2 xzn = normalize(c.xz);
		vec2 cy = vec2(length(c.xz), c.y);
		vec2 s = sign(cy);
		vec2 d = abs(cy) - rim;
		float m = max(d.x, d.y);
		if(m > 0.0)
		{
			n = vec3(0.0, 0.0, 0.0);
		}
		else if(d.x == m)
		{
			n.xz = xzn * s.x;
			n.y = 0.0;
		}
		else
		{
			n = vec3(0.0, s.y, 0.0);
		}
		vec2 p = max(d, 0.0);
		vec2 pn = normalize(p);
		n.xz += s.x * pn.x * xzn;
		n.y += s.y * pn.y;
		return min(m, 0.0) + length(p);
	}

	float roundmin(in float d1, in vec3 n1, in float d2, in vec3 n2, out vec3 n, in float r)
	{
		if(d1 < r && d2 < r)
		{
			vec2 d = r - vec2(d1, d2);
			vec2 dn = normalize(d);
			n = dn.x * n1 + dn.y * n2;
			return r - length(d);
		}
		else if(d1 < d2)
		{
			n = n1;
			return d1;
		}
		else
		{
			n = n2;
			return d2;
		}
	}

	float roundmax(in float d1, in vec3 n1, in float d2, in vec3 n2, out vec3 n, in float r)
	{
		if(d1 > -r && d2 > -r)
		{
			vec2 d = vec2(d1, d2) + r;
			vec2 dn = normalize(d);
			n = dn.x * n1 + dn.y * n2;
			return length(d) - r;
		}
		else if(d1 > d2)
		{
			n = n1;
			return d1;
		}
		else
		{
			n = n2;
			return d2;
		}
	}
	
	void main()
	{
		vec3 r = normalize(raytransform * p);
		vec3 c = eyeposition;
		float d;
		vec3 n;
		for(int i = 0; i < MAXITER; i++)
		{
			float d1, d2, d3;
			vec3 n1, n2, n3;
			d1 = torus(c, n1, 4.0, 0.8);
			d2 = box(c, n2, vec3(4.0, 0.4, 0.4))-0.1;
			d3 = roundmin(d1, n1, d2, n2, n3, 0.2);
			d1 = box(c, n1, vec3(0.4, 0.4, 4.0))-0.1;
			d2 = roundmin(d3, n3, d1, n1, n2, 0.2);
			d3 = sphere(c, n3, 1.0);
			d1 = roundmin(d2, n2, d3, n3, n1, 0.2);
			d2 = cylinder(c, n2, vec2(0.5, 2.0));
			d = roundmax(d1, n1, -d2, -n2, n, 0.2);
			if(d < EPSILON) break;
			if(d > ESCAPE) break;
			c += r*d;
		}
		vec3 norm = normalize(n);
		if(d < EPSILON)
		{
			gl_FragColor = vec4(((1.0 + norm.y) * ROSE_QUARTZ + (1.0 - norm.y) * SERENITY) * 0.5
				* (AMBIENT + (1.0 - AMBIENT) * max(0.0, dot(norm, normalize(LIGHT)))), 1.0);
		}
		else
		{
			gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
		}
	}
</script>
<script type="text/javascript">
	function draw() {
		try
		{
			var canvas = document.getElementById("webgl");
			var gl = canvas.getContext("experimental-webgl");
			if (!gl) { throw "WebGL canvas failed"; }
		}
		catch (err)
		{
			throw "WebGL not supported";
		}
		var prog = gl.createProgram();
		attachShaderFromID = function(type, id)
			{
				var s = gl.createShader(type);
				gl.shaderSource(s, document.getElementById(id).text);
				gl.compileShader(s)
				if(!gl.getShaderParameter(s, gl.COMPILE_STATUS))
				{
					throw "Shader not compiled:\n" + gl.getShaderInfoLog(s);
				}
				gl.attachShader(prog, s);
			}
		attachShaderFromID(gl.VERTEX_SHADER, "vertex_shader");
		attachShaderFromID(gl.FRAGMENT_SHADER, "fragment_shader");
		gl.linkProgram(prog);
		if (!gl.getProgramParameter(prog, gl.LINK_STATUS))
		{
			throw "Shader program not linked";
		}
		gl.useProgram(prog);

		var eye = [2, 10, 5];
		var focus = [0, 0, 0];
		var etof = new Array(3);
		for(var i = 0; i < 3; i++) etof[i] = focus[i] - eye[i];
		var etofxz = Math.sqrt(etof[0] * etof[0] + etof[2] * etof[2]);
		var etofxyz = Math.sqrt(etof[0] * etof[0] + etof[1] * etof[1] + etof[2] * etof[2]);
		gl.uniform1f(gl.getUniformLocation(prog, "aspectratio"), canvas.width / canvas.height);
		gl.uniform1f(gl.getUniformLocation(prog, "fieldofview"), 1.2);
		gl.uniform3fv(gl.getUniformLocation(prog, "eyeposition"), eye);
		gl.uniformMatrix3fv(gl.getUniformLocation(prog, "raytransform"), false, [
			etof[2]/etofxz,0,-etof[0]/etofxz,
			-etof[0]*etof[1]/(etofxz*etofxyz),etofxz/etofxyz,-etof[2]*etof[1]/(etofxz*etofxyz),
			etof[0]/etofxyz, etof[1]/etofxyz, etof[2]/etofxyz]);

		gl.bindBuffer(gl.ARRAY_BUFFER, gl.createBuffer());
		gl.bufferData(gl.ARRAY_BUFFER,
			new Float32Array([-1, 1, 0, 1, 1, 0, -1, -1, 0, 1, -1, 0]), gl.STATIC_DRAW);
		var attr = gl.getAttribLocation(prog, "pos");
		gl.enableVertexAttribArray(attr);
		gl.vertexAttribPointer(attr, 3, gl.FLOAT, false, 0, 0);
		gl.drawArrays(gl.TRIANGLE_STRIP, 0, 4);
	}

	function init()
	{
		try
		{
			draw();
		}
		catch (e)
		{
			alert("Error:\n" + e);
		}
	}
	
	setTimeout(init, 100);
</script>
<center>
<h1>WebGL Raycasting Experiment</h1>
<h2>by Kyle Gagner</h2>
<canvas id="webgl" width="640" height="480"></canvas>
</center>
</body>
</html>
